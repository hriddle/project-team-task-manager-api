plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'edu.depaul.taskmanager'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

configurations {
    localImplementation
}

dependencies {
    implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    implementation 'cz.jirutka.spring:embedmongo-spring:1.3.1'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    localImplementation 'com.newrelic.agent.java:newrelic-java:5.0.0@zip'

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat 'full'
        showExceptions true
        showCauses true
        showStackTraces true
        afterSuite { desc, result ->
            if (!desc.parent) {
                println()
                println "TEST RESULTS"
                println "---------------"
                println "Successful  ${result.successfulTestCount}"
                println "Skipped     ${result.skippedTestCount}"
                println "Failed      ${result.failedTestCount}"
                println "---------------"
            }
        }
    }
}

task unzip(type: Copy) {
    from configurations.localImplementation
    into "$rootDir/newrelic"
}

build.dependsOn unzip
